# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ash/webui/tools/mwc_path_mappings.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Sample System Web App is ash-chrome only")

ts_library("trusted_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"

  in_files = [
    "inter_frame_communication.ts",
    "main.ts",
    "page_handler.ts",
    "timer.ts",
    "component_playground.ts",
  ]

  composite = true

  # Allows TSC to check the generated mojom-webui/ directory when it encounters
  # imports.
  path_mappings =
      [ "/*|" + rebase_path("$root_gen_dir/mojom-webui/*", target_gen_dir) ]

  foreach(_mwc_mapping, mwc_path_mappings) {
    path_mappings +=
        [ _mwc_mapping[0] + "|" + rebase_path(_mwc_mapping[1], target_gen_dir) ]
  }

  deps = [
    ":worker_ts",
    "../../mojom:mojom_ts",
    "//third_party/cros-components:cros_components_ts",
    "//third_party/material_web_components:bundle_lit_ts",
    "//third_party/material_web_components:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
  ]
}

ts_library("worker_ts") {
  tsconfig_base = "tsconfig_worker.json"
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"

  in_files = [ "worker.ts" ]

  composite = true
}

generate_grd("trusted_grd") {
  # Note: Don't forget to:
  #
  #  1. Allocate resource id in //tools/gritsettings/resource_ids.spec for the
  #     .grd file
  #  2. Generate the resource pack (a .pak file) from the .grd file generated
  #     here in //ash/webui/resources/BUILD.gn
  #  3. Add the generated pack (from step 1) to //chrome/chrome_paks.gni
  #
  # Otherwise you'll see network errors when loading the resources.
  grd_prefix = "ash_sample_system_web_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  input_files = [
    "app_icon_192.png",
    "component_playground.html",
    "index.html",
    "inter_frame_communication.html",
    "share.html",
    "timer.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  # trusted_ts.manifest is generated by :trusted_ts and adds all of the in_files
  # to the grd.
  manifest_files =
      filter_include(get_target_outputs(":trusted_ts"), [ "*_manifest.json" ])
  manifest_files +=
      filter_include(get_target_outputs(":worker_ts"), [ "*_manifest.json" ])

  # Flatten out the dependency tree of your mojom and add generated bindings
  # grdp files here.
  # TODO(crbug.com/1002798): We could add a mojo_grdp_deps variable to
  # generate_grd that automatically adds these grdp files when you add a
  # X_webui_grdp target to it.
  grdp_files = [
    "$target_gen_dir/../../mojom/shared_webui_resources.grdp",
    "$target_gen_dir/../../mojom/trusted_webui_resources.grdp",
  ]

  deps = [
    ":trusted_ts",
    ":worker_ts",
    "../../mojom:shared_webui_grdp",
    "../../mojom:trusted_webui_grdp",
  ]
}
