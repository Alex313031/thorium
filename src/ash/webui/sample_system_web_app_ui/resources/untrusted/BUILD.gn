# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Sample System Web App is ash-chrome only")

ts_library("untrusted_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"

  in_files = [
    "untrusted.ts",
    "untrusted_page_interface.ts",
  ]

  composite = true

  # Allows TSC to check the generated mojom-webui/ directory when it encounters
  # imports.
  path_mappings =
      [ "/*|" + rebase_path("$root_gen_dir/mojom-webui/*", target_gen_dir) ]

  deps = [ "../../mojom:mojom_ts" ]
}

generate_grd("untrusted_grd") {
  # Note: Don't forget to:
  #
  #  1. Allocate resource id in //tools/gritsettings/resource_ids.spec for the
  #     .grd file
  #  2. Generate the resource pack (a .pak file) from the .grd file generated
  #     here in //ash/webui/resources/BUILD.gn
  #  3. Add the generated pack (from step 1) to //chrome/chrome_paks.gni
  #
  # Otherwise you'll see network errors when loading the resources.
  grd_prefix = "ash_sample_system_web_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  input_files = [ "untrusted.html" ]
  input_files_base_dir = rebase_path(".", "//")

  # untrusted_ts.manifest is generated by :untrusted_ts and adds all of the
  # in_files to the grd.
  manifest_files =
      filter_include(get_target_outputs(":untrusted_ts"), [ "*_manifest.json" ])

  # Flatten out the dependency tree of your mojom and add generated bindings
  # grdp files here.
  # TODO(crbug.com/1002798): We could add a mojo_grdp_deps variable to
  # generate_grd that automatically adds these grdp files when you add a
  # X_webui_grdp target to it.
  grdp_files = [
    "$target_gen_dir/../../mojom/shared_webui_resources.grdp",
    "$target_gen_dir/../../mojom/untrusted_webui_resources.grdp",
  ]

  deps = [
    ":untrusted_ts",
    "../../mojom:shared_webui_grdp",
    "../../mojom:untrusted_webui_grdp",
  ]
}
